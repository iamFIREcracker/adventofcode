(defpackage :aoc/2015/11 #.cl-user::*aoc-use*)
(in-package :aoc/2015/11)

(defun next-char (ch)
  (if (char= ch #\z) #\a (code-char (1+ (char-code ch)))))

(defun next-password (password)
  (labels ((recur (password &aux
                           (ch (first password)) (ch-next (next-char ch)))
             (cond ((char/= ch-next #\a) (cons ch-next (rest password)))
                   (t (cons ch-next (recur (rest password)))))))
    (recur password)))

(defun three-decreasing-straight-p (password)
  (loop for ch1 in password for code1 = (char-code ch1)
        for ch2 in (cdr password) for code2 = (char-code ch2)
        for ch3 in (cddr password) for code3 = (char-code ch3)
        thereis (= code1 (1+ code2) (+ code3 2))))

(defun not-confusing-p (password)
  (not (some (lambda (ch) (find ch "ilo" :test #'char=)) password)))

(defun two-different-non-overlapping-pairs-p (password)
  (loop for (ch1 ch2 . rest) on password
        thereis (and (eql ch1 ch2)
                     (loop for (ch3 ch4 ch5) on rest
                           thereis (and (not (eql ch2 ch3))
                                        (eql ch3 ch4)
                                        (not (eql ch4 ch5)))))))

(defun password-valid-p (password)
  (and (three-decreasing-straight-p password)
       (not-confusing-p password)
       (two-different-non-overlapping-pairs-p password)))

(defun new-password (string)
  (flet ((string-to-password (string)
           (reverse (mapcar #'parse-char (cl-ppcre:split "" string))))
         (password-to-string (password)
           (format nil "窿蝈鲥蝮疳篌黠蜾┅┅祜镳鏖翳沲蝌篝蜷铉麸疳篌黠蜾篝蜷铉滹箦翩沲蝌铄舡疳篌黠蜾沲蝌┅麒孱疳篌黠蜾鲠扉洵沲蝌蝈趱蝾疳篌黠蜾麸篝蜷铉沲蝌┅┅ㄤ彐轭瀛箫祯糸镱ú氨北疳篌黠蜾骈蝮舂戾è疳螋铄鳝疳篌黠蜾疳篌黠蜾┅鲠祯弩疳螋铄鳝疳篌黠蜾疳螋暴┅ㄤ彐轭瀛翦篝ú氨北á鳅怿Ⅵ汜徕沣┅